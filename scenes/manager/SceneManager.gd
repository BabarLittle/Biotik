"""=============================================
File: SceneManager
Author: Babar
Version: 0.2
Description:
	Script that controls the switching between the different maps, the menu 
	scenes and battle scenes
	NOTE: menu screens manage themselves once loaded
	
	Structure of the algorithm : 
		1. Player enters door / leaves area / press MenuButton
		2. current_scene enters "exit_scene()" GDScriptFunctionState
			- Saves whathever data that needs to be transferred to next_scene
			- Emit "Signal_SceneChanging" to the SceneManager
		3. SceneManager enters "handle_scene_changed"
			- Load and hide the next scene
			- Play the correct Animation
		4. AnimationPlayer signals the end of the Animation
		5. SceneManager acts according to the animation name
			- "FadeToBlack" delete current scene and makes the next one VisibilityEnabler
		6. Reset scene variables and set new ones

Changes:
	0.1 (Babar)
		- File creation (Babar)
	0.2.2023.3.4 (Ska)
		- Changed functions to make the whole file more dynamic (Ska)
		- Added code annotations
	
To-do:
	- Implement funky and dynamics transitions to battle scene
	- Add switching sounds
	- Review / redo the function '_on_AnimationPlayer_animation_finished()'
============================================="""

extends Node2D

# Setting constants and enums
enum TransitionType {NEW_SCENE, MENU_SCREEN, BATTLE_SCREEN, TITLE_SCREEN}

# Setting variables
var scene_parameters = {} # mandatory dictionnary to pass parameters between scenes
var next_scene = null # Next scene of type String
var transition_type = TransitionType.NEW_SCENE

# Setting onready var
onready var current_scene = $CurrentScene/Town # First scene of the game

"""=====
Function _ready()
Author: Ska
	- Initialise variables :
		-> Connect signal from the first scene (current_scene)
====="""
func _ready() -> void:
	current_scene.connect("Signal_SceneChanging", self, "handle_scene_changing")

"""=====
Function handle_scene_changing()
Author: Ska
	- Occurs when receiving the signal "Signal_SceneChanged".
	- Effectivly switch the scene once it's loaded.
	
Arguments:
	- next_level_name: String passed by the current scene to determines wich scene is the next
	- transition_name: String passed by the current scene to choose the animation played
====="""
func handle_scene_changing(next_scene_name: String, transition_name:String="FadeToBlack"):
	""" Check if the scene exists """
	var check_scene_name = File.new()
	assert(check_scene_name.file_exists(next_scene_name), "The specified scene '%s%%' does not exist!" % next_scene_name)
	
	""" Check if the animation exists """
	assert($ScreenTransition/AnimationPlayer.has_animation(transition_name), "The specified animation '%s%%' does not exist in the AnimationPlayer!" % transition_name)
	
	""" Main body of the function """
	print("Balise main handle_scene_changed")
	next_scene = load(next_scene_name).instance()
	next_scene.visible = false
	$ScreenTransition/AnimationPlayer.play(transition_name)
	
	""" Determines what the next scene is """
	if "UI" in next_scene_name:
		transition_type = TransitionType.MENU_SCREEN
	elif "environnement" in next_scene_name:
		transition_type = TransitionType.NEW_SCENE
	

"""=====
Function _on_AnimationPlayer_animation_finished()
Author: Ska
	- Occurs when receiving the animation ending signal from $ScreenTransition/AnimationPlayer
	
Arguments:
	- anim_name: String that contains the name of the animation that just finished. Automaticly 
	generated by the AnimationPlayer
====="""
func _on_AnimationPlayer_animation_finished(anim_name):
	var next_animation_name = ""
	
	match transition_type:
		TransitionType.NEW_SCENE:
			next_animation_name = "FadeToNormal"
		TransitionType.MENU_SCREEN:
			next_animation_name = "FadeWhiteToNormal"
		TransitionType.BATTLE_SCREEN:
			next_animation_name = "FadeToNormal"
		TransitionType.TITLE_SCREEN:
			next_animation_name = "FadeToNormal"
	
	""" To rewrite... a simpler way might exists """
	match anim_name:
		"FadeToBlack":
			$CurrentScene.add_child(next_scene)
			next_scene.connect("SignalSceneChanging", self, "handle_scene_changing")
			next_scene.load_scene(current_scene.scene_parameters)
			next_scene.visible = true
			current_scene.queue_free()
			current_scene = next_scene
			next_scene = null
			$ScreenTransition/AnimationPlayer.play(next_animation_name)
		"FadeToWhite":
			$CurrentScene.add_child(next_scene)
			next_scene.load_scene(current_scene.scene_parameters)
			next_scene.visible = true
			current_scene.queue_free()
			current_scene = next_scene
			next_scene = null
			$ScreenTransition/AnimationPlayer.play(next_animation_name)
	
	
